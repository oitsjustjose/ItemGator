plugins {
    id "com.modrinth.minotaur" version "2.+"
    id "com.matthewprenger.cursegradle" version "1.4.0+"
    id "net.neoforged.gradle" version "[6.0.18,6.2)"
}

group = 'com.oitsjustjose.itemgator'
version = "${mod_version}"
base { archivesName = "ItemGator-${game_version}" }

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
minecraft {
    mappings channel: 'official', version: "${game_version}"
    copyIdeResources = true
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            mods { itemgator { source sourceSets.main } }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            mods { itemgator { source sourceSets.main } }
        }
    }
}


dependencies {
    minecraft "net.neoforged:forge:${game_version}-${neo_version}"
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version       : game_version,
            minecraft_version_range : game_version_range,
            neo_version             : neo_version,
            neo_version_range       : neo_version_range,
            loader_version_range    : loader_version_range,
            mod_version             : mod_version,
            pack_format_number      : pack_format_number,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "Item Gator",
                "Specification-Vendor"    : "oitsjustjose.com",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${mod_version}",
                "Implementation-Vendor"   : "oitsjustjose.com",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.register('publish') {
    dependsOn 'modrinth'
}

modrinth {
    token = project.hasProperty("MODRINTH_API_KEY") ? project.MODRINTH_API_KEY : ""
    projectId = "VkVpYDxO"
    versionName = "ItemGator ${game_version} ${mod_version}"
    versionNumber = "${game_version}-${mod_version}"
    versionType = "release"
    uploadFile = jar
    gameVersions = ["${game_version}"]
    loaders = ["forge"]
    changelog = file("./changelog.md").text
}
